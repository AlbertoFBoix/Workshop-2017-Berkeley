--***********************************************
--***********************************************
--Documentation for frobeniusPowers.m2
--***********************************************
--***********************************************

doc ///
    Key
        fastExp
        (fastExp, ZZ, RingElement)
    Headline
        Computes powers of elements in rings of positive characteristic quickly.
    Usage
        fastExp(n,f) 
    Inputs
        n:ZZ
            nonnegative
     	f:RingElement
            in positive characteristic
    Outputs
        :RingElement
            the n-th power of f
    Description
        Text
            In prime characteristic p > 0, raising a sum (a+b) to a power is more quickly done by simply computing a^p 
            and b^p and adding them. The basic strategy is to break up the exponent into its base p expansion, and then 
            use the exponent rules. For example, (x+y)^(3*p^2+5*p+2) = ((x+y)^3)^(p^2)*((x+y)^5)^p*(x+y)^2.
        Example
            R = ZZ/5[x];
            f = sum( 10, i -> x^i );
            time f^321;
            time fastExp(321,f);
///

doc ///
    Key
        frobenius
    Headline
        computes Frobenius powers of ideals and matrices.
    Usage
        frobenius(e,I)
        frobenius^e(I) 
	frobenius(e,M)
	frobenius^e(M)
        frobenius(I)
        frobenius(M)
    Inputs
        e:ZZ
        I:Ideal
            in a ring of positive characteristic p > 0
        M:Matrix
            with entries in a ring of positive characteristic p > 0
    Outputs
        :Ideal
	:Matrix
    Description
        Text
	    frobenius(e,I) or 
            frobenius^e(I) returns the p^e-th Frobenius power of I,
            I^[p^e]. If e is nonnegative, then I^[p^e] is the ideal generated by all
            f^(p^e), with f in I.
        Example
            R = ZZ/3[x,y];
            I = ideal(x,y);
            frobenius(2,I)
            frobenius^3(I)            
        Text
            If e is negative, then frobenius(e,I) or frobenius^e(I) computes a "Frobenius
               root", as defined by Blickle, Mustata, and Smith.
            That is, frobenius(e,I) computes frobeniusRoot(-e,I).
        Example
            R = ZZ/5[x,y,z,w];
            I = ideal(x^27*y^10+3*z^28-x^2*y^15*z^35,x^17*w^30+2*x^10*y^10*z^35,x*z^50);
            frobenius(-1,I)
            frobenius(-2,I)
        Text
            If e is a nonnegative integer, then
            frobenius(e,M) or frobenius^e(M) outputs a matrix whose entries are 
            the p^e-th powers of the entries of M.  
        Example
            M = ZZ/3[x,y];
            M = matrix{{x,y},{x+y,x^2+y^2}};
            frobenius(2,M)            
        Text
            frobenius(I) and frobenius(M) are abbreviations for 
            frobenius(1,I) and frobenius(1,M).
///

doc ///
    Key
        frobeniusPower
        ( frobeniusPower, ZZ, Ideal )
        ( frobeniusPower, QQ, Ideal )
    Headline
        computes the (generalized) Frobenius power of an ideal
    Usage
        frobeniusPower(n,I)
     	frobeniusPower(t,I)
    Inputs
        n:ZZ
            nonnegative
    	t:QQ
            nonnegative
        I:Ideal
            in a ring of characteristic p > 0
    Outputs
        :Ideal
    Description
        Text
	    frobeniusPower(t,I) computes the generalized Frobenius power I^[t], 
            as introduced by Hernandez, Teixeira, and Witt.
            If the exponent is a power of the characteristic, this is just the 
            usual Frobenius power: 
        Example
            R = ZZ/5[x,y];
            I = ideal(x,y);
            frobeniusPower(125,I)
        Text
            If n is an arbitrary nonnegative integer, then write the base p expansion
            of n as follows: n = a_0 + a_1*p + a_2*p^2 + ... + a_r*p^r. Then the n-th 
            Frobenius power of I is defined as follows:
            I^[n] = (I^a_0)*(I^a_1)^[p]*(I^a_2)^[p^2]*...*(I^a_r)^[p^r].
        Example
            R = ZZ/3[x,y];
            I = ideal(x,y);
            adicExpansion(3,17)
            J1 = I^2*frobenius(1,I^2)*frobenius(2,I);
            J2 = frobeniusPower(17,I);
            J1 == J2
        Text
            If t is a rational number of the form t = a/p^e, then I^[t] = (I^[a])^[1/p^e].
        Example
            R = ZZ/5[x,y,z];
            I = ideal(x^50*z^95, y^100+z^27);
            frobeniusPower(4/5^2,I)
            frobeniusRoot(2,frobeniusPower(4,I))
        Text
            If t is an arbitrary nonegative rational number, and t_n = a_n/p^(e_n) is 
            a sequence of rational numbers converging to t from above, then 
            I^[t] is the largest ideal in the increasing chain of ideals I^[t_n].
///
{*
        Example
            p = 13;
            R = ZZ/p[x,y];
            I = ideal(x^50,y^30);
            t = 7/19;
            expon = e -> ceiling( p^e*t )/p^e; -- a sequence converging to t from above
            scan( 5, i -> print frobeniusPower(expon(i),I) )
            frobeniusPower(7/19,I,FrobeniusPowerStrategy => Safe)
*}
 
 